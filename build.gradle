plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'org.f14a.fatin2'
version = '2.0.0'

repositories {
    mavenCentral()
}

dependencies {
    // WebSocket client library
    implementation 'org.java-websocket:Java-WebSocket:1.6.0'

    // Json processing library
    implementation 'com.google.code.gson:gson:2.13.2'

    // YAML processing library
    implementation 'org.yaml:snakeyaml:2.5'

    // Logging library
    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation 'ch.qos.logback:logback-classic:1.5.21'

    // Tools
    implementation 'org.apache.commons:commons-lang3:3.18.0'

    // Lombok
//    compileOnly 'org.projectlombok:lombok:1.18.30'
//    annotationProcessor 'org.projectlombok:lombok:1.18.30'

}

application {
    mainClass = 'org.f14a.fatin2.Main'
}

// Create a fat JAR (also known as uber JAR)
task fatJar(type: Jar) {
    archiveBaseName.set('fatin2')
    archiveVersion.set(version)
    archiveClassifier.set('')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes 'Main-Class': application.mainClass
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    with jar

    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

// Standard JAR without dependencies
jar {
    archiveBaseName.set('fatin2')
    archiveVersion.set(version)
    archiveClassifier.set('plain')
}

build.dependsOn fatJar, jar

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task runBot(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.f14a.fatin2.Main'

    // Debug environment
    workingDir = file("$projectDir/run")

    // Create run directory if it doesn't exist
    doFirst {
        file("$projectDir/run").mkdirs()
    }

    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
}
