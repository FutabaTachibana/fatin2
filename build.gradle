plugins {
    id 'java-library'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
    id 'signing'
}

group = 'org.f14a'
version = System.getenv("RELEASE_VERSION") ?: project.version
// Versions are defined in gradle.properties:
// - version (host app)
// - version_demoplugin

// Java version
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

dependencies {
    // Annotations
    implementation 'org.jetbrains:annotations:24.0.0'

    // WebSocket client library
    implementation 'org.java-websocket:Java-WebSocket:1.6.0'

    // Json processing library
    api 'com.google.code.gson:gson:2.13.2'

    // YAML processing library
    implementation 'org.yaml:snakeyaml:2.5'

    // Logging library
    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation 'ch.qos.logback:logback-classic:1.5.21'

    // Tools
    implementation 'org.apache.commons:commons-lang3:3.18.0'

    // Tests
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.3'
}

application {
    mainClass = 'org.f14a.fatin2.Main'
}

components.java.withVariantsFromConfiguration(configurations.shadowRuntimeElements) {
    skip()
}

// --- Custom source sets for internal plugins (built as separate jars) ---
// Plugins have been migrated into independent Gradle subprojects:
// - :demoplugin
// This avoids IDE classpath issues and keeps dependencies strictly scoped per plugin.

// --- Main application artifacts ---
// Adding new plugin source sets wonâ€™t require manually excluding packages.
jar {
    archiveBaseName.set('fatin2')
    archiveVersion.set(project.version.toString())
    // Standard Jar output named as: fatin2-{version}-standard.jar
    archiveClassifier.set('')

    // Only package the main application code
    include 'org/f14a/fatin2/**'

    manifest {
        attributes(
            'Main-Class': application.mainClass.get(),
            'Implementation-Title': project.name,
            'Implementation-Version': project.version
        )
    }
}

// Shadow jar should be the primary runnable distribution.
shadowJar {
    archiveBaseName.set('fatin2')
    archiveVersion.set(project.version.toString())
    // Requirement: shadowJar output named as: fatin2-{version}.jar
    archiveClassifier.set('all')

    manifest {
        attributes(
            'Main-Class': application.mainClass.get(),
            'Implementation-Title': project.name,
            'Implementation-Version': project.version
        )
    }
}

// Ensure normal build produces both host artifacts.
build.dependsOn jar, shadowJar

// --- Plugin jars ---
// Plugin builds moved to subprojects. For convenience, keep aggregate tasks.

// DEPRECATED: Tachibana plugin is now a separate project (not open sources).
// It is used to realize my custom bot's deployment and not public anymore.
//tasks.register('buildTachibana') {
//    group = 'tachibana'
//    description = 'Build the Tachibana plugin shaded JAR (includes plugin dependencies)'
//    dependsOn ':tachibana:shadowJar'
//}

tasks.register('buildDemoplugin') {
    group = 'demoplugin'
    description = 'Build the demo plugin shaded JAR'
    dependsOn ':demoplugin:shadowJar'
}

// Utility task to run the bot (without automatically building plugins)
tasks.register('runBot', JavaExec) {
    group = 'application'
    description = 'Run the Fatin2 bot'

    classpath = sourceSets.main.runtimeClasspath
    mainClass = application.mainClass.get()
    workingDir = file("$projectDir/run")

    doFirst {
        file("$projectDir/run").mkdirs()
    }

    if (project.hasProperty('args')) {
        args project.property('args').toString().split('\\s+')
    }
}

// Clean up generated plugin jars on clean
clean {
    delete "$projectDir/run/plugins/demoplugin-${version_demoplugin}.jar"
}

// Test configuration
test {
    doFirst {
        file("$projectDir/test").mkdirs()
    }
    workingDir = file("$projectDir/test")
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:all,-missing', '-quiet')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'fatin2'
                description = 'A quick and easy-to-use bot framework for Onebot v11.'
                url = 'https://github.com/FutabaTachibana/fatin2'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'FutabaTachibana'
                        name = 'Futaba_Tachibana'
                        url = 'https://github.com/FutabaTachibana'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/FutabaTachibana/fatin2.git'
                    developerConnection = 'scm:git:ssh://git@github.com/FutabaTachibana/fatin2.git'
                    url = 'https://github.com/FutabaTachibana/fatin2'
                }
            }
        }
    }

    repositories {
        maven {
            name = "localStaging"
            url = layout.buildDirectory.dir("staging-deploy")
        }
    }
}

signing {
    // Align with workflow secret names.
    // We keep Gradle signing optional, but CI will set these for Maven Central.
    def signingKey = System.getenv('JRELEASER_GPG_SECRET_KEY') ?: System.getenv('GPG_PRIVATE_KEY')
    def signingPassword = System.getenv('JRELEASER_GPG_PASSPHRASE') ?: System.getenv('GPG_PASSPHRASE')

    required { signingKey != null && !signingKey.isBlank() }

    if (signingKey != null && !signingKey.isBlank()) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenJava
    }
}
